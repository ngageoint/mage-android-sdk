import org.ajoberstar.grgit.Grgit;

plugins {
    id 'org.ajoberstar.grgit' version '1.6.0'
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

group 'mil.nga.giat.mage'
archivesBaseName = 'mage-android-sdk'
version '6.4.0'
ext {
    versionCode = 26
    sourceRefspec = Grgit.open().head().id
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 30
        resValue 'string', 'source_refspec', project.sourceRefspec
        manifestPlaceholders source_refspec: project.sourceRefspec
        buildConfigField 'String', 'SOURCE_REFSPEC', $/"${project.sourceRefspec}"/$
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task sourceJar(type: Jar) {
    from project.android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                from components.release
                artifactId archivesBaseName
                artifact source: sourceJar, classifier: 'sources'
            }
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.preference:preference:1.1.1'
    api 'androidx.exifinterface:exifinterface:1.0.0'
    api 'com.j256.ormlite:ormlite-android:5.1'
    api 'mil.nga.geopackage:geopackage-android:3.4.0'
    api 'com.squareup.retrofit2:retrofit:2.5.0'
    api 'com.squareup.retrofit2:converter-gson:2.5.0'
    api 'com.squareup.retrofit2:converter-jackson:2.5.0'
    api 'com.squareup.okhttp3:okhttp:3.12.0'
    api 'com.squareup.okhttp3:okhttp-urlconnection:3.11.0'
    api 'com.google.code.gson:gson:2.8.2'
    api 'com.google.guava:guava:18.0'
    api 'org.apache.commons:commons-lang3:3.3.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    api files('libs/sanselanandroid.jar')
}
